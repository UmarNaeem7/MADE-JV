composite blocktype XLSXSheetExtractor {

    input inputName oftype None;
    output outputName oftype Sheet;

    inputName
        -> ExcelExtractor
        -> ExcelInterpreter
        -> GDPSheetPicker
        -> GDPDataSelector
        -> HeaderWriter1
        -> HeaderWriter2
        -> outputName;

    block ExcelExtractor oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    block ExcelInterpreter oftype XLSXInterpreter { }

    block GDPSheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    block GDPDataSelector oftype CellRangeSelector {
        select: range P2:S45;
    }

    block HeaderWriter1 oftype CellWriter {
        at: cell A1;
        write: [
            "Country Code"
        ];
    }

    block HeaderWriter2 oftype CellWriter {
        at: range C1:D1;
        write: [
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }
}

pipeline BondPipeline {

    BondExtractor
        -> BondTableInterpreter
        -> BondLoader;

    block BondExtractor oftype XLSXSheetExtractor { }

    block BondTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype Ratio,
        ];
    }

    block BondLoader oftype SQLiteLoader {
        table: "bondIssuance";
        file: "./country-stats.sqlite";
    }

    valuetype PositiveDecimal oftype decimal {
        constraints: [
            ValidatePositiveDecimal
        ];
    }

    valuetype Ratio oftype decimal {
        constraints: [
            ValidateRatio
        ];
    }
}

pipeline GDPPipeline {

    GDPExtractor
        -> GDPTableInterpreter
        -> GDPLoader;

    block GDPExtractor oftype XLSXSheetExtractor { }

    block GDPTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype PositiveDecimal,
        ];
    }

    block GDPLoader oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "./country-stats.sqlite";
    }


    valuetype PositiveDecimal oftype decimal {
        constraints: [
            ValidatePositiveDecimal
        ];
    }

    valuetype Ratio oftype decimal {
        constraints: [
            ValidateRatio
        ];
    }
}

publish constraint ValidatePositiveDecimal on decimal: value>0;

publish constraint ValidateRatio oftype RangeConstraint {
    lowerBound: 0;
    lowerBoundInclusive: true;
    upperBound: 1;
    upperBoundInclusive: true;
}